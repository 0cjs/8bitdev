;   NEC PC-8001 expansion ROM with various programs and experiments.

            cpu Z80
            include  src/i8080/std.i80

; ----------------------------------------------------------------------
;   PC-8001 BIOS

;   Note that prchar (and perhaps other routines) are quite stack-heavy;
;   they may easily use well over 16 bytes (maybe 24 or more?).
prchar      equ  $0257          ; ♠A ♡* print char to CRT
rdchar      equ  $0F75          ; ♠A ♣BCHL wait for char from keyboar

;   ♠A ♣?   print char in A to current output device
rst__outchar macro
            rst  $18
            endm
outchar     equ  $40A6

err_notimpl equ  $1875          ; "Disk BASIC Feature" error routine
basicret    equ  $3C82          ; ??? return to basic after USR, etc.?
omonstart   equ  $5C35          ; enter original ROM monitor

perr_syntax equ  $3BDF          ; "Syntax error"
perr_misop  equ  $3BF4          ; "Missing operand"
perr_type   equ  $3BF7          ; "Type mismatch"
perror      equ  $3BF9          ; ♠C=error code

;   BASIC statement hooks. These are initialised with `jp err_notimpl`.
crt_width   equ  $EA65          ; current text screen width (chars)
sj_00       equ  $F0E1          ; start of statement hook table
sj_CMD      equ  $F0FC          ; jump point for BASIC `CMD`
sj_end      equ  $F1B6          ; end (exclusive) of statement hook table

; ----------------------------------------------------------------------
;   Expansion ROM Header

            org  $6000          ; start of expansion ROM
            db   'AB'           ; signature indicating expansion ROM present
exprom_init jp   init           ; entry point of expansion ROM initialisation

; ----------------------------------------------------------------------
;   General I/O Routines

            include  src/i8080/pr.i80

;   We don't use the BASIC routine because we need this before it's init'd.
prstr       ld   a,(hl)
            inc  hl             ; move past terminator before exit
            or   a,a
            ret  Z
            call prchar
            jr   prstr

prnl        push af
            ld   a,'\r'
            call prchar
            ld   a,'\n'
            call prchar
            pop  af
            ret

; ----------------------------------------------------------------------
;   ROM Init

init        ld   hl,.message    ; print "ROM installed" banner
            call prstr
            ;   All statement hook table entries enter tmon.
            ;   • This is a temporary thing to help us debug which statement
            ;     hooks correspond to which statements, and can be removed
            ;     once that's properly documented.
            ;   • XXX We need to test if Disk BASIC overwrites our settings
            ;     of the statement hooks, calls them before/after, or just
            ;     leaves them alone.
            ld   d,MB(intentry)
            ld   e,LB(intentry)
            ld   hl,sj_00
            ;   Actually, the statement hook table, which we'd calculate as
            ;   b,(sj_end-sj_00)/3, is probably too long, since entries
            ;   near the end are filled with RET, and other things in the
            ;   BIOS call these and expect no failure. So instead we use
            ;   the $2E from the ROM routine that inits the table.
            ld   b,$2E
-           inc  hl             ; skip past JMP
            ld   (hl),e         ; address LSB
            inc  hl
            ld   (hl),d         ; address MSB
            inc  hl
            djnz -
            ;
            ;   Fill in specific commands for which we have other actions.
            ld   hl,cmd         ; hook BASIC `CMD` statement
            ld   (sj_CMD+1),hl
            ret                 ; continue main ROM init
            ;
.message    db '8bitdev expansion ROM #1.\r\n',0

; ----------------------------------------------------------------------
;   BASIC Statement Hooks
;
;   These are entered through statement hook table entries, with:
;   • HL→ The remainder of the command line, tokenized (leading spaces
;     skipped, converted to upper case, tokens substituted, $00-terminated).
;   • DE→ Address of command processing routine in `iltbl`.
;   • BC→ Address of statement hook.
;   The cursor is left at start of a new line.
;
;   XXX Not clear how we return error versus not. RET sometimes generates
;   a "Syntax error," sometimes not; it must depend on flags or something.
;   for the moment, we hack it by using `basicret`, but that may be
;   wrong, especially when it comes to stack handling.

;   Parse argument to `CMD` statement and run matching routine.
;   This is a bogus little Q&D routine that checks only the first
;   character given as an argument to `CMD`.
cmd         ;   HL→cmdargs,0
            ld  a,(hl)
            or  a,a             ; No argument?
            jp  Z,perr_misop    ;   yes: "Missing operand"
            ld   de,cmdtab_str
            call strtabi        ; match a valid argument?
            jp   NZ,+           ;   yes: execute matching routine
            ld   e,5            ; "Illegal function call"
            jp   perror
            ;
+
            ld   hl,cmdtab_addr
            ld   a,c
            add  a,a            ; byte index → word index
            add  a,l            ; index into table
            ld   l,a
            jp   NC,+           ; add carry if necessary (Z80 jr is shorter)
            inc  h
+           ld   a,(hl)         ; load address from table
            inc  hl
            ld   h,(hl)
            ld   l,a
            jp   (hl)

            include  src/i8080/strtabi.i80

cmdtab_str  db   'HELP',0
            db   'HELLO',0
            db   'T',$B7,0       ; "TMON": $B7 = `MON` token
            db   0
cmdtab_addr dw   cmd_help
            dw   cmd_hello
            dw   cmd_tmon

; ----------------------------------------------------------------------
;   Minimal "Hello, world."

cmd_help    ld   hl,.message
            call prstr
            jp   basicret

.message    db   'CMD HELP|HELLO|TMON\r\n',0

; ----------------------------------------------------------------------
;   A more sophisticated "Hello, world" that reads and writes chars.
;
;   XXX this has its own prstr, which should be extracted.

cmd_hello   call hello
            jp   basicret

    section hello
            public hello
            include src/i8080/hello.i80
    endsection hello

; ----------------------------------------------------------------------
;   tmon
;
;   XXX TODO:
;   - prvischar and prscreenchar need to be set up for inverse (and maybe
;     prvischar should be using that printable BS, etc.)
;   - Add RST for monitor entry.

cmd_tmon

    section tmon
            public entry
            public intentry

tmon_rom    equ  $

;   Start our stack/work area at the first byte after the work area used by
;   `MON`. Nothing after the MON work area is used after initialisation.
;   Note we allocate plenty of stack because the ROM routines use quite a
;   bit: tmon has been seen to use 42 bytes when calling prchar and the like.
tmon_ram    equ  $FF3D
tmon_stksz  equ  $FF80-tmon_ram  ; 67 bytes stack

BACKSPACE   equ  $1D            ; cursor left

            include src/i8080/tmon.i80

            include src/i8080/prvischar/pc8001.i80      ; XXX no reverse
            include src/i8080/prscreenchar/ascii.i80    ; XXX no reverse

exit        ld   a,(t0)
            dec  a          ; a=1 ?
            jp   nz,+       ;    no: try next value
            ;
            ;   Enter the standard ROM monitor (original `MON`) and restart
            ;   tmon when done. This is somewhat more convenient for us
            ;   because, unlike going back to BASIC, no re-init of things
            ;   is done (so we can more easily examine the results of what
            ;   was done in the original monitor) and we can get back to
            ;   BASIC easily enough with `q`uit anyway.
            call omonstart  ;   yes: call original ROM monitor
                            ;        through entry point that returns to us
            jp   entry      ;        and return to us
            ;
+           jp   basicret   ; 0 or any other value re-enters BASIC

BEL         equ  $07
errbeep     ld   a,BEL
            jp   prchar         ; RET

init_platform
            ld   a,72-1         ; min. screen width for 16-bytes/line examine
            ld   hl,crt_width
            cp   (hl)           ; min-1 ≥ current width
            ret  c              ;   yes: use default examine width
            ld   a,8            ;    no: switch to 8 bytes/line
            ld   (vW_examine),a
            ret

    endsection tmon

; ----------------------------------------------------------------------
;   End of Expansion ROM.

    if $ > $7FFC
            error 'code PC=$\{$} > expansion RAM max $\{tmon_ram_end}'
    endif

            org  $7FFC
            jp   intentry       ; entry point used by `MON`
            db   $55            ;   if this magic byte set to $55
