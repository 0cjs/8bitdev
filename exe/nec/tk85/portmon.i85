;   portmon: Monitor current values read from an I/O space address
;
;   This continuously reads the port number shown in the left-hand side
;   of the display and on the right, after a turnstile, displays the value
;   last read. (E.g., `08 ⊢ FF`) The INC and DEC keys will increment and
;   decrement the port number (wrapping around at $FF/$00), and you may
;   also type two digits on the hex keypad to set a port number directly.
;
;   This is intended to aid in debugging hardware projects.

        cpu 8085
        include  src/i8080/std.i80
TK85    equ  1
        include  src/biosdef/tk80.i80

;   Pointer to a routine that that will read a byte based on an argument
;   in A, and return the value read in A. Typically this will be a routine
;   that reads a the I/O port given in A, but it can be anything you like.
readbyte    equ  $8300

;   The current argument to `readbyte`.
port        equ  $8302

;   The routine that actually loads data from a given port, which must
;   be in RAM because the port number itself is modified.
in_port     equ  $8303

; ----------------------------------------------------------------------

            org  $1800

init        ld   a,$00          ; initial port
            ld   (port),a
            call read_port_init
            ld   hl,read_port
            ld   (readbyte),hl
            nop
            nop

init1       ;   Clear display.
            ld   a,0
            ld   hl,segments
            ld   c,8
-           ld   (hl),a
            inc  hl
            dec  c
            jp   NZ,-
            ;   Show '⊢' indicating value.
            ld   a,$70
            ld   (segments+4),a

read        ld   hl,.dispval    ; return addr
            push hl             ;   for simulated call
            ld   a,(port)
            ld   e,3            ; print port in left side of display
            call prbyte
            ld   a,(port)       ; read port value
            ld   hl,(readbyte)
            jp   (hl)
.dispval    ld   e,7            ; rightmost LED display
            call prbyte
            ;
            call getkey         ; keypress available?
            cp   a,key_rinc     ; next port?
            jp   NZ,+
            ld   a,(port)
            inc  a
            jp   .newport
+           cp   a,key_rdec     ; previous port?
            jp   NZ,+
            ld   a,(port)
            dec  a
            jp   .newport
+           cp   a,$10          ; it's a number?
            jp   NC,read        ;    no: read and update display
            ;
            ld   e,2            ; display left digit of new port being entered
            call prnyb
            rlca                ; move digit to upper nybble
            rlca
            rlca
            rlca
            ld   (port),a       ; save upper nybble
            ld   a,$00          ; clear port display lower nybble
            ld   (segments+3),a
-           call waitkey        ; get next keypress
            cp   a,$10          ; number?
            jp   NC,-           ;    no: ignore it
            ld   hl,port
            or   a,(hl)         ; mask in upper nybble
.newport    ld   (port),a       ; save new port
            ld   e,3            ; display right digit of new port number
            call prbyte
            ;
            jp   read

;   ♠AE ♣ABCDHL   Print A as two digits at the LED position given in E
;   (lower nybble) and the LED position to its left (upper nybble).
prbyte      call prnyb          ; print lower nybble
            rrca                ; shift upper nybble to lower
            rrca
            rrca
            rrca
            ; FALLTHROUGH       ; RET (TCO) and print it

;   ♠AE ♣BCDHL   Print the digit in the lower nybble of A at the LED
;   position given by E (0-7). A is preserved, and E will be decremented on
;   return to have the next call print a more significant digit.
prnyb       push af             ; preserve value
            ld   hl,charsegs    ; glyph table base address
            ld   b,$00          ; index MSB
            and  a,$0F          ; mask lower nybble
            ld   c,a            ; index LSB
            add  hl,bc          ; index into glyph table
            ld   a,(hl)         ; load glyph
            ld   hl,segments    ; leftmost LED position
            ld   d,$00          ; LED position MSB
            add  hl,de          ; move up to requested segment
            ld   (hl),a         ; display glyph
            dec  e              ; previous LED position
            pop  af             ; restore input value
            ret

; ----------------------------------------------------------------------

;   ♠A  Return A+1. Used for testing the main routine.
read_Aplus1 inc  a
            ret

;   Set up/modify the routine in RAM that does an arbitrary port read.
read_port_init
            ld   hl,in_port     ; routine address
            ;
            ld   a,$DB          ; IN instruction
            ld   (hl),a
            inc  hl
            ;
            ld   a,(port)       ; port number (modified later)
            ld   (hl),a
            inc  hl
            ;
            ld   a,$C9          ; RET instruction
            ld   (hl),a
            ret

read_port   ld   (in_port+1),a
            jp   in_port        ; RET (TCO)
