.TITLE APPLE SERIAL LOADER
STATUS=$C0AE
DATA=$C0AF
.=$2000
INIT:	LDA #$03
	STA STATUS		;INIT ACIA
	LDA #$10
	STA STATUS		;SET BAUD RATE TO HIGH
SLOAD:	JSR GCHAR		;GET A CHAR FROM SERIAL LINE
	CMP #';			;START OF A BLOCK?
	BNE SLOAD		;IF NOT, GET ANOTHER CHAR
	JSR GBYTE		;GET BLOCK LENGTH
	TAX			;SAVE IN X
	BEQ DONE		;IS THIS THE LAST BLOCK?
	JSR GBYTE		;GET THE START ADR OF BLOCK
	STA $02
	JSR GBYTE
	STA $01
	LDY #$00
DALOOP:	JSR GBYTE		;GET THE DATA BYTE
	STA ($01),Y		;PUT IT IN MEMORY
	INY			;INCREMENT THE MEMORY OFFSET
	DEX			;DECREMENT THE BYTE COUNT
	BNE DALOOP		;END OF BLOCK?
	BEQ SLOAD		;IF SO, GO GET ANOTHER
GCHAR:	LDA STATUS
	LSR A
	BCC GCHAR
	LDA DATA
	AND #$7F
DONE:	RTS
GBYTE:	JSR GHEX
	ASL A
	ASL A
	ASL A
	ASL A
	STA $00
	JSR GHEX
	ORA $00
	RTS
GHEX:	JSR GCHAR
	CMP #$30
	BMI BARF
	CMP #$3A
	BPL NOTNUM
	AND #$0F
	RTS
NOTNUM:	CMP #$41
	BMI BARF
	CMP #$47
	BPL BARF
	AND #$07
	CLC
	ADC #$09
	RTS
BARF:	BRK
.=$2100
	LDX #$0D
TLOOP:	LDA STATUS
	AND #$02
	BEQ TLOOP
	STX DATA
	RTS
.END
