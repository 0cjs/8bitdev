;   Apple II: Print the charset used by COUT

            cpu     6502
            include "src/a2/biosdef.a65"

SPACE       equ     AA(' ')

            org     $800

            ldx #0
nextchar    txa
            and #$1F            ; divisible by 32?
            bne +               ; no, just print the char

            ;   Start new line prefixed with first value in hex.
            jsr CROUT
            txa
            jsr PRBYTE
            lda #AA(':')
            jsr COUT

+           ;   COUT prints nothing for most control codes ($80-$9F), and a
            ;   few generate actions such as beeping or cursor movement.
            ;   Skip the entire row since we get nothing useful from it.
            txa
            cmp #$80            ; <$80?
            bcc +               ; yes, print it
            cmp #SPACE          ; â‰¥SPACE?
            bcs +               ; yes, print it
            ldx #SPACE          ; skip to space
            bmi nextchar        ; BRA because SPACE has high bit set

+           jsr COUT
            inx
            bne nextchar
            jsr CROUT
            jmp $3D0            ; return to DOS
