;   Simple 6502 routines to test

            ;   All numbers are in hex unless specified otherwise.
            ;   However, we still need to prefix numbers starting with
            ;   A-F with `0` or `$$` so they're not confused with symbols.
            .radix h

;---------------------------------------------------------------------
            ;   This is one way of specifying the location of the code,
            ;   but experimentation has shown that it's probably better
            ;   to leave all code and data in the default _CODE area
            ;   and specify `-b _CODE=0xNNNN` to the linker to do this.
            ;
            ;   XXX But the test framework must be fixed to properly
            ;   find the locations of reloaded symbols, first.
            ;
            .area   CODE (abs)
            .org    400
            .end    400         ; entry point

            ;   Zero page area, allowing us to interleave allocations
            ;   in this with the code.
            .area   ZP  (abs,pag)
            .org    80               ; allocated area starts 80 bytes in

;---------------------------------------------------------------------
            .area   CODE

ident:      .str    "simple.a65"

;   Add X and Y, storing the result in xybuf and returning it in A
            .globl addxy,xybuf
addxy:      txa
            sty xybuf
            clc
            adc xybuf
            sta xybuf
            rts
xybuf:      .ds     1

;----------------------------------------------------------------------
;   JMP [addr] vs PHA/RTS
;   See Woz's Sweet 16 article for details.
;   Enter with the index (0-3) of the jmplist routine in A.

jmpptr      =       0010        ; zero page

jmpabs:     asl                 ; convert index to word offset
            tax
            lda     jmplist,X   ; destination addr LSB
            sta     jmpptr
            inx
            lda     jmplist,X   ; destination addr MSB
            sta     jmpptr+1
            jmp     [jmpptr]

;   This is a table of entry points for routines.
;   WARNING: It must not cross a page boundary!
jmplist:    .dw     1234
            .dw     5678
            .dw     9abc
            .dw     $$def0      ; Starts with letter = read as symbol

;   Stores the address _before_ the routine, to avoid an add in jmpabsrts
jmprtslist: .dw     1234-1
            .dw     5678-1
            .dw     9abc-1
            .dw     $$def0-1    ; Starts with letter = read as symbol

jmpabsrts:  asl                 ; convert index to word offset
            tax
            lda     jmprtslist+1,X ; dest addr MSB
            pha
            lda     jmprtslist,X   ; dest addr LSB
            pha
            rts

;---------------------------------------------------------------------
;   A source of bytes: each call returns in A the next byte from
;   [bytesource], incrementing bytesource after. Preserves X and Y.
;   This is a bit awkward; if we were worried about efficiency we'd
;   read from [bytesource+1], among other things.

bsread:     sty     bs_ysave
            ldy     #0
            lda     [bytesource],y
            pha
            ldy     bs_ysave
            inc     bytesource
            bne     1$
            inc     bytesource+1
1$:         pla
            rts

            .area   ZP
bytesource: .ds     2       ; pointer to bytes to read
bs_ysave:   .ds     1       ; temp storage for Y register
            .area   CODE
