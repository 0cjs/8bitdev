;   Sharp MZ-series tmon loaded just below 48K
;
;   On an MZ-80K this will load at the top of RAM. On later models, it
;   loads well below the top of RAM, but underneath the bank-switched
;   $D000-$FFFF area.

            cpu Z80
            include  src/i8080/std.i80

tmon_rom    equ  $C700
tmon_ram    equ  $CF00
MINSTACKSZ  equ  $80
tmon_stack  equ  $D000

mzinit      equ  tmon_rom - 8

; ----------------------------------------------------------------------
;   `.mzf` Tape Header, until we make a program that can build this.

mzf         org  mzinit - $80   ; tape header is 128 bytes
.type       db   $01
.filename   db   'TMON48'
            db   17 - ($ - .filename) dup CR
.size       dw   end - mzinit
.start      dw   mzinit
.entry      dw   mzinit

; ----------------------------------------------------------------------
;   BIOS routines

LETNL       equ  $0006
PRNTS       equ  $000C
PRNT        equ  $0012
GETKY       equ  $001B
BELL        equ  $003E

prnl        equ  LETNL
prchar      equ  PRNT
prspace     equ  PRNTS

; ----------------------------------------------------------------------

BACKSPACE   equ  $14            ; cursor left

; ----------------------------------------------------------------------

            org  mzinit
            call LETNL
            jp   intentry       ; do we have a caller to which we can return?

        if $ >= tmon_rom
            error 'mzinit overflow'
        endif

            org  tmon_rom
            include src/i8080/tmon.i80

rdchar      call GETKY
            or   a,a
            jr   z,rdchar
            cp   a,$66
            ret  nz
            ld   a,CR
            ret


            include src/i8080/prvischar/ascii.i80       ; XXX no reverse
            include src/i8080/prscreenchar/ascii.i80    ; XXX no reverse

errbeep     jp   BELL       ; TCO ret

exit        jp   $0082      ; MZ-80K monitor prompt/command loop
            ;   XXX This should check to:
            ;     a) see if we're on an MZ-80A or MZ-700 and go to the
            ;        right place for those
            ;     b) see if S-BASIC or HUBASIC is loaded and re-enter that.

init_platform
            ;   Here is where we should determine if we're on an MZ-80K,
            ;   MZ-80A or MZ-700, and whether S-BASIC (or maybe HUBASIC)
            ;   is loaded so we can reserve our memory space and know
            ;   what to do with the `q` command.
            ret

; ----------------------------------------------------------------------
;   Checks to ensure things don't overflow their allocated space.
;   • Code doesn't overflow into work area.
;   • MINSTACKSZ bytes available for stack above work area.

    if $ > tmon_ram
            error 'code PC=$\{$} > tmon_ram=$\{tmon_ram}'
    endif

    if tmon_ram_end > tmon_stack - MINSTACKSZ
            error 'tmon_ram_end=$\{tmon_ram_end} > max stack $\{tmon_stack - MINSTACKSZ}'
    endif

; ----------------------------------------------------------------------

end         end mzinit
