;   pristr: print inline string

;   Index base address to get the addr that JSR pushed on the stack.
;   TSX and then load this indexed with X to load the LSB.
;   (Use irtsaddr+1 for the MSB.)
;   Remember! The pushed address is ONE LESS than the address to which
;   we will return.
;
irtsaddr    EQU $101

;---------------------------------------------------------------------
;   External Configuration: the following must be defined by the client
;
;   prchar: print the character in A.
;
;   pristrP: a zero page word for the address for indirect access to
;   data in calling code. We do not care what this is used for between
;   calls to pristr.

;   Abort assembly with a clear message if an essential definition is missing.
    set ______,
    pushv , prchar, pristrP
    popv  , ______, ______

;---------------------------------------------------------------------
;   â™£AX pristr: print inline string
;
;   Using `prchar`, print the $00-terminated string from memory immediately
;   after the call to this subroutine. (This requires a JSR.) There is no
;   limit on the size of the string.
;
;   This requires one word of zero-page storage, pristrP, for only the
;   duration of each call to this routine. It may destroy additional
;   registers from the `prchar` call.
;
pristr      tsx                     ; copy retaddr-1 to pristrP
            lda irtsaddr,X          ;   LSB
            sta pristrP
            lda irtsaddr+1,X        ;   MSB
            sta pristrP+1
            ldx #0
.nextbyte   inc pristrP             ; increment data pointer
            bne +
            inc pristrP+1
+           lda (pristrP,X)         ; next char; is it 0?
            beq .done               ;   yes: we're finished printing
            jsr prchar
            clc                     ; same size, so might as well
            bcc .nextbyte           ;   make jump relocatable

.done       ;   Fixup return address. We did not increment past the $00
            ;   terminator, so it is the correct retaddr-1 expected by RTS.
            tsx
            lda pristrP
            sta irtsaddr,X
            lda pristrP+1
            sta irtsaddr+1,X
            rts
