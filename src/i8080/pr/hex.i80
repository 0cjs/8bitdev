;   pr/hex: hexadecimal output of value
;
;   Requires: `prchar`, which must preserve all registers but AF.

; ----------------------------------------------------------------------
;   ♠A ♣A ♡*   Print value in HL as 4 hex digits.
prhexHL     ld   a,h
            call prhexA
            ld   a,l
            ; fallthrough

;   ♠A ♣A ♡*   Print value in A as 2 hex digits.
prhexA      push af
            rrca
            rrca
            rrca
            rrca
            call prnyb
            pop  af
            ; fallthrough

;   ♠A ♣A ♡*   Print lower nybble of A as 1 hex digit.
;
prnyb       and  $0F
            add  a,'0'
            cp   '9'+1
            jp   C,.digit
            add  a,'A'-'9'-1
.digit      jp   prchar         ; ret (TCO)

; ----------------------------------------------------------------------
;   Debugging Macros (no code space used if not used).
;   Since we've got the hex print routines around anyway, make available
;   some macros useful for debugging code in emulators etc.

pdebugA     macro {NOEXPAND}
            push AF             ; preserve flags
            call prhexA
            call prspace
            pop AF
            endm

pdebugHL    macro {NOEXPAND}
            push AF             ; preserve flags
            call prhexHL
            call prspace
            pop AF
            endm
